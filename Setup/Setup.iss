; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
AppName=ClamWin Antivirus
AppVerName=ClamWin Antivirus 0.1c
AppPublisher=Alex Cherney
AppPublisherURL=http://www.cher.id.au/winclamav
AppSupportURL=http://www.cher.id.au/winclamav
AppUpdatesURL=http://www.cher.id.au/winclamav
DefaultDirName={pf}\ClamWin
DefaultGroupName=ClamWin Antivirus
LicenseFile=SetupFiles\License.rtf
AllowNoIcons=yes

ShowLanguageDialog=yes
LanguageDetectionMethod=locale
OutputDir=Output
Compression=lzma/ultra
InternalCompressLevel=max

SolidCompression=true
[Files]
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

Source: cygwin\cygbz2-1.dll; DestDir: {app}\bin; Components: Cygwin
Source: cygwin\cyggmp-3.dll; DestDir: {app}\bin; Components: Cygwin
Source: cygwin\cygwin1.dll; DestDir: {app}\bin; Components: Cygwin
Source: cygwin\cygz.dll; DestDir: {app}\bin; Components: Cygwin
Source: py2exe\dist\bin\python23.dll; DestDir: {app}\bin; Components: ClamWin
Source: py2exe\dist\bin\Tray.exe; DestDir: {app}\bin; Components: ClamWin
Source: py2exe\dist\bin\ClamWin.exe; DestDir: {app}\bin; Components: ClamWin
Source: py2exe\dist\bin\img\Clam.png; DestDir: {app}\bin\img; Components: ClamWin
Source: py2exe\dist\bin\img\Control.png; DestDir: {app}\bin\img; Components: ClamWin
Source: py2exe\dist\bin\img\FrameIcon.ico; DestDir: {app}\bin\img; Components: ClamWin
Source: py2exe\dist\bin\img\PythonPowered.gif; DestDir: {app}\bin\img; Components: ClamWin
Source: py2exe\dist\bin\img\World.png; DestDir: {app}\bin\img; Components: ClamWin
Source: py2exe\dist\bin\img\wxPyButton.png; DestDir: {app}\bin\img; Components: ClamWin
Source: py2exe\dist\bin\img\wxWidgButton.png; DestDir: {app}\bin\img; Components: ClamWin
Source: py2exe\dist\lib\_socket.pyd; DestDir: {app}\lib; Components: ClamWin
Source: py2exe\dist\lib\_sre.pyd; DestDir: {app}\lib; Components: ClamWin
Source: py2exe\dist\lib\_ssl.pyd; DestDir: {app}\lib; Components: ClamWin
Source: py2exe\dist\lib\datetime.pyd; DestDir: {app}\lib; Components: ClamWin
Source: py2exe\dist\lib\htmlc.pyd; DestDir: {app}\lib; Components: ClamWin
Source: py2exe\dist\lib\unicodedata.pyd; DestDir: {app}\lib; Components: ClamWin
Source: py2exe\dist\lib\w9xpopen.exe; DestDir: {app}\lib; Components: ClamWin
Source: py2exe\dist\lib\win32api.pyd; DestDir: {app}\lib; Components: ClamWin
Source: py2exe\dist\lib\win32event.pyd; DestDir: {app}\lib; Components: ClamWin
Source: py2exe\dist\lib\win32file.pyd; DestDir: {app}\lib; Components: ClamWin
Source: py2exe\dist\lib\win32gui.pyd; DestDir: {app}\lib; Components: ClamWin
Source: py2exe\dist\lib\win32pipe.pyd; DestDir: {app}\lib; Components: ClamWin
Source: py2exe\dist\lib\win32process.pyd; DestDir: {app}\lib; Components: ClamWin
Source: py2exe\dist\lib\win32trace.pyd; DestDir: {app}\lib; Components: ClamWin
Source: py2exe\dist\lib\clamwin.zip; DestDir: {app}\lib; Components: ClamWin
Source: py2exe\dist\lib\wxc.pyd; DestDir: {app}\lib; Components: ClamWin
Source: py2exe\dist\lib\wxmsw24h.dll; DestDir: {app}\lib; Components: ClamWin
Source: py2exe\dist\lib\zlib.pyd; DestDir: {app}\lib; Components: ClamWin
Source: ..\clamav-devel\clamscan\clamscan.exe; DestDir: {app}\bin; Components: ClamAV
Source: ..\clamav-devel\freshclam\freshclam.exe; DestDir: {app}\bin; Components: ClamAV
Source: ..\clamav-devel\sigtool\sigtool.exe; DestDir: {app}\bin; Components: ClamAV
Source: {%TEMP|{localappdata}}\clamwin-src.zip; DestDir: {app}\src; Components: Sources; Flags: external
Source: {%TEMP|{localappdata}}\clamav-src.tar.gz; DestDir: {app}\src; Components: Sources; Flags: external
;Source: {%TEMP|{localappdata}}\cygwin-src.tar.bz2; DestDir: {app}\src; Components: Sources; Flags: external
Source: ..\cpp\Build\ExplorerShell.dll; DestDir: {app}\bin; Components: ExplorerShell
Source: py2exe\dist\bin\WClose.exe; DestDir: {app}\bin; Components: ClamWin
Source: py2exe\dist\lib\pywintypes23.dll; DestDir: {app}\lib; Components: ClamWin
Source: py2exe\dist\lib\w9xpopen.exe; DestDir: {app}\bin; Components: ClamWin
[Icons]
Name: {group}\Virus Scanner; Filename: {app}\bin\ClamWin.exe; WorkingDir: {app}\bin; Comment: ClamWin Antivirus; Components: ClamWin
Name: {userdesktop}\ClamWin Antivirus; Filename: {app}\bin\ClamWin.exe; WorkingDir: {app}\bin; Comment: ClamWin Antivirus; Components: ClamWin; Tasks: desktopicon

[Run]
; NOTE: The following entry contains an English phrase ("Launch"). You are free to translate it into another language if required.

Filename: {app}\bin\ClamWin.exe; Parameters: --mode=update --close; WorkingDir: {app}\bin; StatusMsg: Downloading Virus Database Files; Components: ClamWin; Tasks: DownloadDB
Filename: {app}\bin\Tray.exe; WorkingDir: {app}\bin; Flags: nowait; Components: ClamWin
[Dirs]
Name: {app}\db; Components: ClamAV
Name: {app}\bin; Components: ExplorerShell ClamWin Cygwin
Name: {app}\lib; Components: ClamWin
Name: {app}\bin\img; Components: ClamWin

Name: {app}\src; Components: Sources
Name: {app}\tmp; Flags: uninsalwaysuninstall; attribs: hidden
[_ISTool]
OutputExeFilename=L:\Projects\ClamWin\Setup\Output\setup.exe
[Languages]
Name: Default; MessagesFile: compiler:\Default.isl; LicenseFile: Setupfiles\License.rtf
[Components]
Name: Cygwin; Description: Cygwin Files; Flags: fixed; Types: full custom typical
Name: ClamAV; Description: ClamAV Files; Flags: fixed; Types: full custom typical
Name: ClamWin; Description: ClamWin Files; Flags: fixed; Types: full custom typical
Name: ExplorerShell; Description: Integration with Windows Explorer; Types: full custom typical
Name: Sources; Description: Download Source Code; ExtraDiskSpaceRequired: 9437184; Types: full
[INI]
Filename: {app}\bin\ClamWin.conf; Section: ClamAV; Key: clamscan; String: {app}\bin\clamscan.exe
Filename: {app}\bin\ClamWin.conf; Section: ClamAV; Key: freshclam; String: {app}\bin\freshclam.exe
Filename: {app}\bin\ClamWin.conf; Section: ClamAV; Key: database; String: {app}\db

[_ISToolDownload]
Name: Sources_ClamWin; Description: ClamWin Source Code; GroupDescription: Source Code; Flags: unchecked; Source: http://osdn.dl.sourceforge.net/sourceforge/clamwin/clamwin-0.1c-src.zip; DestDir: {%TEMP|{localappdata}}; DestName: clamwin-src.zip; Components: Sources
Name: Sources_ClamAV; Description: ClamAV Source Code; GroupDescription: Source Code; Flags: unchecked; Source: http://osdn.dl.sourceforge.net/sourceforge/clamav/clamav-0.70-rc.tar.gz; DestDir: {%TEMP|{localappdata}}; DestName: clamav-src.tar.gz; Components: Sources
;Name: Sources_Cygwin; Description: Cygwin Source Code; GroupDescription: Source Codes; Flags: unchecked; Source: http://www.cher.id.au/winclamav/download/src/cygwin-src.tar.bz2; DestDir: {%TEMP|{localappdata}}; DestName: cygwin-src.tar.bz2; Components: Sources

[Types]
Name: typical; Description: Typical Installation
Name: custom; Description: Custom Installation; Flags: iscustom
Name: full; Description: Full Installation
[UninstallDelete]
Name: {app}\bin\ClamWin.conf; Type: files; Components: ClamWin
Name: {app}\db; Type: filesandordirs; Components: ClamAV
[Registry]
Root: HKLM; Subkey: Software\Microsoft\Windows\CurrentVersion\Run; ValueType: string; ValueName: ClamWin; ValueData: {app}\bin\Tray.exe; Flags: uninsdeletevalue; Components: ClamWin
Root: HKLM; Subkey: Software\ClamWin; ValueType: string; ValueName: Path; ValueData: {app}\bin; Flags: uninsdeletekey deletevalue; Components: ClamWin
Root: HKCR; Subkey: CLSID\{{65713842-C410-4f44-8383-BFE01A398C90}\InProcServer32; ValueType: string; ValueData: {app}\bin\ExplorerShell.dll; Flags: uninsdeletekey; Components: ExplorerShell
Root: HKCR; Subkey: CLSID\{{65713842-C410-4f44-8383-BFE01A398C90}\InProcServer32; ValueType: string; ValueData: Apartment; Flags: uninsdeletekey; ValueName: ThreadingModel; Components: ExplorerShell
Root: HKCR; Subkey: *\shellex\ContextMenuHandlers\ClamWin; ValueType: string; ValueData: {{65713842-C410-4f44-8383-BFE01A398C90}; Flags: uninsdeletekey; Components: ExplorerShell
Root: HKCR; Subkey: Folder\shellex\ContextMenuHandlers\ClamWin; ValueType: string; ValueData: {{65713842-C410-4f44-8383-BFE01A398C90}; Flags: uninsdeletekey; Components: ExplorerShell
Root: HKLM; Subkey: SOFTWARE\Cygnus Solutions; Flags: uninsdeletekeyifempty; Check: NoCygWin
Root: HKLM; Subkey: SOFTWARE\Cygnus Solutions\Cygwin; Flags: uninsdeletekeyifempty; Check: NoCygWin
Root: HKLM; Subkey: SOFTWARE\Cygnus Solutions\Cygwin\Program Options; Flags: uninsdeletekeyifempty; Check: NoCygWin
Root: HKLM; Subkey: SOFTWARE\Cygnus Solutions\Cygwin\mounts v2; Flags: uninsdeletekeyifempty; Check: NoCygWin
Root: HKLM; Subkey: SOFTWARE\Cygnus Solutions\Cygwin\mounts v2\/tmp; ValueType: string; ValueName: native; ValueData: {app}\tmp; Flags: uninsdeletekeyifempty uninsdeletevalue; Check: NoCygWin
Root: HKLM; Subkey: SOFTWARE\Cygnus Solutions\Cygwin\mounts v2\/tmp; ValueType: dword; ValueName: flags; ValueData: 10; Flags: uninsdeletekeyifempty uninsdeletevalue; Check: NoCygWin
[Tasks]
Name: DownloadDB; Description: Download Virus Database Files; GroupDescription: Download; Components: ClamAV
; NOTE: The following entry contains English phrases ("Create a desktop icon" and "Additional icons"). You are free to translate them into another language if required.
Name: desktopicon; Description: Create a &desktop icon; GroupDescription: Additional icons:; Flags: unchecked
[UninstallRun]
Filename: {app}\bin\WClose.exe; WorkingDir: {app}\bin
[Code]
var
  CygwinChecked, CygwinFound: Boolean;

// Function generated by ISTool.
function NextButtonClick(CurPage: Integer): Boolean;
begin
	Result := istool_download(CurPage);
end;

procedure InitializeWizard();
var
	uninstall: String;
	keyname: String;
	resultcode: Integer;
begin
	keyname := 'SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\WinClamAV Antivirus_is1';
	if not RegKeyExists(HKEY_LOCAL_MACHINE, keyname) then begin
		exit;
	end;
	if not RegQueryStringValue(HKEY_LOCAL_MACHINE, keyname, 'UninstallString',  uninstall) then begin
		exit;
	end;
	if MsgBox('The Setup detected previous version of the software is already installed on this computer. Would you like to uninstall it now?',
	              mbConfirmation, MB_YESNO) = idYes then begin
		InstExec(RemoveQuotes(uninstall), ' /SILENT', '', True, False, SW_SHOWNORMAL, resultcode);
	end;
end;

procedure DeInitializeSetup();
begin
	DeleteFile(ExpandConstant('{%TEMP|{localappdata}}\clamwin-src.zip'));
	DeleteFile(ExpandConstant('{%TEMP|{localappdata}}\clamav-src.tar.gz'));
	;DeleteFile(ExpandConstant('{%TEMP|{localappdata}}\cygwin-src.tar.bz2'));
end;

function NoCygwin(): Boolean;
begin
	if not CygWinChecked then begin
		CygwinFound := RegKeyExists(HKEY_LOCAL_MACHINE, 'SOFTWARE\Cygnus Solutions\Cygwin\mounts v2');
		CygwinChecked := True;
		Result := not CygwinFound;
	end
	else begin
		Result := not CygwinFound;
	end
end;

begin
	CygwinChecked := False;
	CygwinFound := False;
end.
